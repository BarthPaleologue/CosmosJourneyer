precision lowp float;

varying vec3 vPosition; // position of the vertex in sphere space

uniform vec3 starColor;
uniform float time;

// Noise functions to spice things up a little bit
float mod289(float x){return x - floor(x * (1.0 / 289.0)) * 289.0;}
vec4 mod289(vec4 x){return x - floor(x * (1.0 / 289.0)) * 289.0;}
vec4 perm(vec4 x){return mod289(((x * 34.0) + 1.0) * x);}

float noise(vec3 p){
    vec3 a = floor(p);
    vec3 d = p - a;
    d = d * d * (3.0 - 2.0 * d);

    vec4 b = a.xxyy + vec4(0.0, 1.0, 0.0, 1.0);
    vec4 k1 = perm(b.xyxy);
    vec4 k2 = perm(k1.xyxy + b.zzww);

    vec4 c = k2 + a.zzzz;
    vec4 k3 = perm(c);
    vec4 k4 = perm(c + 1.0);

    vec4 o1 = fract(k3 * (1.0 / 41.0));
    vec4 o2 = fract(k4 * (1.0 / 41.0));

    vec4 o3 = o2 * d.z + o1 * (1.0 - d.z);
    vec2 o4 = o3.yw * d.x + o3.xz * (1.0 - d.x);

    return o4.y * d.y + o4.x * (1.0 - d.y);
}

float completeNoise(vec3 p, int nbOctaves, float decay, float lacunarity) {
	float totalAmplitude = 0.0;
	float value = 0.0;
	for(int i = 0; i < nbOctaves; ++i) {
		totalAmplitude += 1.0 / pow(decay, float(i));
		vec3 samplePoint = p * pow(lacunarity, float(i)); 
		value += noise(samplePoint) / pow(decay, float(i));
	}
	return value / totalAmplitude;
}

vec3 lerp(vec3 a, vec3 b, float t) {
	return a + t * (b - a);
}

void main() {
	// la unitPosition ne prend pas en compte la rotation de la planÃ¨te
	vec3 unitPosition = normalize(vPosition);

	unitPosition += vec3(time, -time, time) / 100.0;
	
	float noiseValue = completeNoise(unitPosition * 20.0, 8, 2.0, 2.0);

	vec3 finalColor = starColor;

	finalColor -= vec3(pow(noiseValue, 4.0));

	gl_FragColor = vec4(finalColor, 1.0); // apply color and lighting	
} 